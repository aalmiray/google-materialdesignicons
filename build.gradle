import java.text.SimpleDateFormat

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6'
    }
}

apply plugin: 'base'

subprojects { subproj ->
    subproj.apply plugin: 'java'
    subproj.apply plugin: 'maven-publish'
    subproj.apply plugin: 'com.jfrog.bintray'

    def pomConfig = {
        name subproj.name
        description subproj.name
        url 'https://github.com/google/material-design-icons'
        inceptionYear '2014'
        licenses {
            license([:]) {
                name 'Attribution-ShareAlike 4.0 International'
                url 'http://creativecommons.org/licenses/by-sa/4.0/'
                distribution 'repo'
            }
        }
        scm {
            url 'https://github.com/google/material-design-icons/'
        }
    }

    archivesBaseName = 'materialdesignicons-' + subproj.name

    jar {
        manifest {
            attributes(
                'Built-By': System.properties['user.name'],
                'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
                'Build-Date': buildDate,
                'Build-Time': buildTime,
                'Specification-Title': subproj.archivesBaseName,
                'Specification-Version': subproj.version,
                'Implementation-Title': subproj.archivesBaseName,
                'Implementation-Version': subproj.version,
            )
        }
    }

    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenCustom(MavenPublication) {
                from components.java
                pom.withXml {
                    asNode().children().last() + pomConfig
                }
            }
        }
    }

    if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
    if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''

    bintray {
        user = project.bintrayUsername
        key = project.bintrayApiKey
        publications = ['mavenCustom']
        pkg {
            repo = 'kordamp'
            userOrg = 'aalmiray'
            name = rootProject.name
            desc = rootProject.name
            licenses = ['Attribution']
            labels = ['iconpack']
            websiteUrl = 'https://github.com/aalmiray/google-materialdesignicons/'
            issueTrackerUrl = 'https://github.com/aalmiray/google-materialdesignicons/issues'
            vcsUrl = 'https://github.com/aalmiray/google-materialdesignicons.git'
            publicDownloadNumbers = true
        }
    }
}